# Original Maintainer: Gustau Castells <karasu@antergos.com>
# Maintainer: manuel at endeavouros.com
# Contributor: joekamprad <joekamprad@endeavouros.com>

pkgname=nvidia-inst
pkgver=23
pkgrel=9
pkgdesc="Script to setup nvidia drivers (dkms version) in EndeavourOS"
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url=https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname
arch=('any')
license=('GPL2')
replaces=('nvidia-installer-dkms' 'nvidia-installer-db')
depends=(
  nvidia-installer-common
  dkms
)
optdepends=(
  "linux-headers: for $pkgname with kernel linux"
  "linux-lts-headers: for $pkgname with kernel linux-lts"
  "linux-hardened-headers: for $pkgname with kernel linux-hardened"
  "linux-zen-headers: for $pkgname with kernel linux-zen"
)

source=(
  $_url/$pkgname
  $_url/$pkgname.bash-completion
)
sha512sums=('56c80d2c481a2c693633abc2a66b0941d2fb410e93b2faacfea8f9c5754a2e7793e340fe12bbddee4364935a48cd8d055d17e94a41b3fefa8d67b6c0c1609be3'
            'b8857ff98870ac8cff78e31c660bde3b7308752ee4e6547682340866ee59f5801f09ede41e490380a40926536f47259f5d347fd59e8e2ffa67a8cb57f07c44e6')

package() {
    local src file

    for src in "${source[@]}" ; do
        file="${src##*/}"
        case "$src" in
            *.bash-completion) install -Dm644 "$file"  "$pkgdir/usr/share/bash-completion/completions/${file%.*}" ;;
            *)                 install -Dm755 "$file"  "$pkgdir/usr/bin/$file"
        esac
    done
}
