# Maintainer: joekamprad <joekamprad at endeavouros.com>

pkgname=endeavouros-theming
pkgver=24.06.1.1
pkgrel=1
pkgdesc='Default EndeavourOS background images and icons'
arch=('any')
replaces=('EndeavourOS-theming')
url='https://www.endeavouros.com'
license=('GPL3')
makedepends=('git')
source=("https://github.com/endeavouros-team/endeavouros-theming/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('ea19e7e99f57ec8eb5d61801ece49c054fccb3c3afe91e913c197d49dc693f68421e6526c7c95e46bdbfefb6b011200ebf3fd58a7d6227f24e409c06198d4757')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm644 *.png *.svg -t "${pkgdir}/usr/share/endeavouros"
  install -Dm644 endeavouros.{svg,png} -t "${pkgdir}/usr/share/pixmaps"
  install -Dm644 endeavouros-icon.png  -t "${pkgdir}/usr/share/pixmaps"
  install -Dm644 endeavouros-dark.{svg,png} -t "${pkgdir}/usr/share/pixmaps"
  install -Dm644 endeavouros-logo-text.svg -t "${pkgdir}/usr/share/pixmaps"
  install -Dm644 endeavouros-logo-text-dark.svg -t "${pkgdir}/usr/share/pixmaps"

  if [ "${CARCH}" = "aarch64" ]; then
    install -Dm644 backgrounds/endeavouros-arm-wallpaper.png "${pkgdir}/usr/share/endeavouros/backgrounds/endeavouros-wallpaper.png"
  else
    install -Dm644 backgrounds/endeavouros-wallpaper.png "${pkgdir}/usr/share/endeavouros/backgrounds/endeavouros-wallpaper.png"
  fi

  # make symlinks for KDE and GNOME:
  ln -s "/usr/share/pixmaps/endeavouros.png" "${pkgdir}/usr/share/pixmaps/endeavouros-logo.png"
  ln -s "/usr/share/pixmaps/endeavouros.svg" "${pkgdir}/usr/share/pixmaps/endeavouros-logo.svg"
  ln -s "/usr/share/pixmaps/endeavouros-dark.png" "${pkgdir}/usr/share/pixmaps/endeavouros-logo-dark.png"
  ln -s "/usr/share/pixmaps/endeavouros-dark.svg" "${pkgdir}/usr/share/pixmaps/endeavouros-logo-dark.svg"
}
