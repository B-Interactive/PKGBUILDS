#!/bin/bash

# Maintainer: EndeavourOS-Team <info at endeavouros.com>

# Ex-Maintainer:  Nissar Chababy <funilrys at outlook dot com>
# Ex-Maintainer:  Jeroen Bollen <jbinero at gmail dot comau>

pkgname=ckbcomp
pkgrel=1
pkgdesc="Compile a XKB keyboard description to a keymap suitable for loadkeys or kbdcontrol"
arch=(any)
license=('GPL2')
depends=('perl')
sha512sums=('e246168b909added076e98d5521348887d3b948ba4a06ab21ed9104eaefc2e63073d5130e5733fb92bc3e4af25e2222698a5edd391d04e27bf3bd14fce7a0fec')

_ckbcomp_preparations() {
    # Fetch 'ckbcomp' from the latest sources.
    # Out: url, pkgver, source
    # Notes:
    # - salsa.debian.org seems slower...
    # - pkgver may include only numbers, lowercase letters, and "." "+" "~"

    local -r err="echo $pkgname: error:"
    local INFO

    if [ -x /bin/$pkgname ] ; then
        url="https://salsa.debian.org/installer-team/console-setup/raw/master/Keyboard/$pkgname"
        curl --fail -Lsm 30 -o$pkgname "$url" || exit 1
        if diff $pkgname /bin/$pkgname >/dev/null ; then
            echo "$pkgname: no change detected!" >&2
            rm -f $pkgname
            exit 1
        fi
    fi
    url="https://sources.debian.org/src/console-setup"
    INFO=$(curl --fail -Lsm 30 "$url") || { $err "fetching version info failed"; exit 1; }
    pkgver="$(echo "$INFO" | grep -m1 "/src/console-setup/" | sed -E 's|.*/src/console-setup/([^/]+)/.*|\1|')"
    [ "${pkgver//[a-z0-9.+~]/}" ] && { $err "sorry, pkgver ($pkgver) is invalid"; exit 1; }
    source=("https://sources.debian.org/data/main/c/console-setup/$pkgver/Keyboard/$pkgname")
}

_ckbcomp_preparations
unset -f _ckbcomp_preparations

package() {
    install -Dm755 $pkgname   "${pkgdir}/usr/bin/$pkgname"
    rm -f ../$pkgname   # cleanup
}
