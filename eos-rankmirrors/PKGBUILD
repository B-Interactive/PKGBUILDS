# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=eos-rankmirrors
pkgver=2.14
pkgrel=1
pkgdesc="EndeavourOS mirror ranking tool"
arch=('any')

# url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

license=(GPL3)
depends=(eos-bash-shared)
options=(!strip !emptydirs)
backup=(etc/$pkgname.conf)

source=(
  $url/$pkgname
  $url/$pkgname.completion
  $url/eos-rankmirrors-from-hook
  $url/eos-rankmirrors.hook
  $url/eos-rankmirrors.conf
)
sha512sums=('8e7a13619b343d61dd4683b5e119d7c6a41de0fd9d156008144ae4b2250ab65bcbba9135ba50a6aef5b4a1a4800fbb0c3470127a8f9eb36431c06848dd7d1e0f'
            'e0cc7bbd4644574810c19b18cb57098f62011aeb2f5b115a16d78489efbf7f74eab411bd31eaf233e4380916b4890472dd23e17ccc0ef4a0f456321c414dc9e2'
            '580511c676cf56b5b8580d6819e0d1a4c85a40a3cd10f4b4f04fb9c004b14025b56cf4c3048e0fe8f9eef48d924b32b97d1e9669fac39b96f4203e2d68e3bd07'
            'f01f6267f624992febfec29c76f2de1c503693086ade5783e2b9cbef359f5155bf6df520ff06299193c842aef2a05cac5078977ee0dabf55cff66f44aec6e44a'
            '449aa70661ad04010d44a22fda78de1be8f858d71ed087c3579d7ccd962902db748ef7d8db6abf695b72f192ed816f8f974ecd61e8b47a95d823fa550ca9cda0')

package() {
  local hooks=$pkgdir/usr/share/libalpm/hooks

  install -Dm644 eos-rankmirrors.hook      $hooks/eos-rankmirrors.hook

  install -Dm644 eos-rankmirrors.conf      $pkgdir/etc/eos-rankmirrors.conf

  install -Dm755 $pkgname                  $pkgdir/usr/bin/$pkgname
  install -Dm755 eos-rankmirrors-from-hook $pkgdir/usr/bin/eos-rankmirrors-from-hook

  install -Dm644 $pkgname.completion       $pkgdir/usr/share/bash-completion/completions/$pkgname
}
