# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=eos-rankmirrors
pkgver=24
pkgrel=1
pkgdesc="EndeavourOS mirror ranking tool"
arch=('any')

# url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

license=(GPL3)
depends=(eos-bash-shared)
options=(!strip !emptydirs)
backup=(etc/$pkgname.conf)

source=(
  $url/$pkgname
  $url/$pkgname.completion
  $url/eos-rankmirrors-from-hook
  $url/eos-rankmirrors.hook
  $url/eos-rankmirrors.conf
  $url/eos-rankmirrors.disabled
)
sha512sums=('0edc04542fa95c97c031555a8993dc1be291d4eae38ef19c8a634249870552cff4d62adca843580941ab2d4015f65216d34fa2172ed21fa35e38c1f09f6db550'
            'ccfe87b9107618578042dd1379bc080f8df9da85b0a03c0a2b8ddfbd2c2a6a417964b3a83c74a949063aa5c145e8083a69e05bade59cfc938467ea08fb63d945'
            '580511c676cf56b5b8580d6819e0d1a4c85a40a3cd10f4b4f04fb9c004b14025b56cf4c3048e0fe8f9eef48d924b32b97d1e9669fac39b96f4203e2d68e3bd07'
            'f01f6267f624992febfec29c76f2de1c503693086ade5783e2b9cbef359f5155bf6df520ff06299193c842aef2a05cac5078977ee0dabf55cff66f44aec6e44a'
            'a95c38c8d359b054a77a11dec6ea774e3e4c62dcb7f667eaf24e62a2066f12e98da43d2dbf622fd0b9efcce15e6a8c1655afd8dc753c0705b527891f74f8b479'
            '06b77d1109fec45071dd2a3c1be07c225a5735961bd2818d0ccea608fef6e85bf4ae71d821427aa766d5b1934195282c1964965df7bd49602316b3bab6bf9824')

package() {
  local hooks=$pkgdir/usr/share/libalpm/hooks

  install -Dm644 eos-rankmirrors.hook      $hooks/eos-rankmirrors.hook

  install -Dm644 eos-rankmirrors.conf      $pkgdir/etc/eos-rankmirrors.conf
  install -Dm644 eos-rankmirrors.disabled  $pkgdir/etc/eos-rankmirrors.disabled

  install -Dm755 $pkgname                  $pkgdir/usr/bin/$pkgname
  install -Dm755 eos-rankmirrors-from-hook $pkgdir/usr/bin/eos-rankmirrors-from-hook

  install -Dm644 $pkgname.completion       $pkgdir/usr/share/bash-completion/completions/$pkgname
}
