# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=24.12.3
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    systemd eos-bash-shared eos-translations
    eos-qogir-icons
    yad
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
  'kdiff3: alternative app for pacdiff to use'
  'meld: alternative app for pacdiff to use'
  'diffuse: alternative app for pacdiff to use'
  'vim: alternative app for pacdiff to use'
  'eos-quickstart: quick start with installing apps'
)

# url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
# _url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
# https://github.com/endeavouros-team/welcome/raw/main/LICENSE
# https://raw.githubusercontent.com/endeavouros-team/welcome/main/LICENSE
url=https://github.com/endeavouros-team/welcome
_url=https://raw.githubusercontent.com/endeavouros-team/welcome/main

source=(
  $_url/$pkgname
  $_url/${pkgname}-installer.conf
  $_url/$pkgname.bash-completion
  $_url/$pkgname.desktop
  $_url/wallpaper-once
  $_url/wallpaper-once.desktop
  $_url/eos-kill-yad-zombies
  $_url/welcome-dnd
  $_url/eos-install-mode-run-calamares
  $_url/arm-eos-welcome-installer
)
sha512sums=('c55ddc62aeee9874abf67aab7102ae009e523dce1b513dc39d06e91d965356436966bc8ab43e3b12bb7518e4e5e391160f9097edbd1f24ae433ce38a65df7f16'
            '6c53b41475a08e647296ba7d638885b806b6ff61faddeb954211de0b8f4f6dfacf9bdf002d7958c3e05c4cc7d62f150df62a3e32aadd411c8912fc4114da8710'
            '36dcdd5db1d86b88a87c4e48b718b0986107980fc289dd8dd643a365508255a81680d197442dfe6933bbb8b06e2ccc7d7b55bf28ce1a925cdfd0278c6fee2c7f'
            '5ca4b7108573a980d9d4011c5afde4f459abe8e36f5b6bc846368dcc48cfa2e672133f074fdeac4282835fa5b87f7afb2cd9dc3e985b4d2e8f574ac239c90ba2'
            'acd528bafe16d61690d532961da562b0e9238153aa609c7d84d719a19525afe191b9cd54294b837d1d81c4d6cc6c9ae766712174aecb3708314e869addc6b537'
            'a193a605d95d837db568dab2feb074c035bea12bc7b08a39a7be3b075dd063cd05fb46b4bde5c86f81a26456c0aab3b0b834cb1ffd88820095a14291b4e059a5'
            '90dfc1f08a428787305d72a2e439a377aee0bc095fcb0a2efb4245a15285d4b96b5e0ac644c02f27ab68aebbfc5c64e6ab7d733d9db8952d2999d589410d3b9e'
            '9f0cd5edabf93439656046c60dc6a29f439a9d8b7afab8ec03b9470321ba98fc53b927944e2f68b5b152c150cca76913490eaab4256a5699da5960f860d81e81'
            '66f528f4b31382c812cbab6eef9506fd7c3fb78788d95d4ca702ad075308fb0babdc2b4fc05bd8da8ca36e5d07fe1dd317cba350c3fadd8f327eb1b9f0dcb16a'
            'cbc39b4806cbfc6a81ddad7cc174d3850f82699c46c949a3ac9855f3c63f6cd6e91102ca357dc4176c7edcb02682e10b0175a95adcede81ec23a7ee84ab231aa')
package() {
    local -r _eosdir=usr/share/endeavouros/scripts
    local _src _name

    for _src in "${source[@]}" ; do
        _name=${_src##*/}
        case "$_name" in
            *.bash-completion)           install -Dm644 "$_name"  "$pkgdir/usr/share/bash-completion/completions/${_name%.*}" ;;
            *.desktop)                   install -Dm644 "$_name"  "$pkgdir/etc/xdg/autostart/$_name" ;;
            *.conf)                      install -Dm644 "$_name"  "$pkgdir/etc/$_name" ;;
            "$pkgname" | wallpaper-once) install -Dm755 "$_name"  "$pkgdir/$_eosdir/$_name" ;;
            *)                           install -Dm755 "$_name"  "$pkgdir/usr/bin/$_name" ;;
        esac
    done

    # create symlink: /usr/bin/eos-welcome -> /usr/share/endeavouros/scripts/welcome
    ln -s "/$_eosdir/$pkgname"   "$pkgdir/usr/bin/eos-$pkgname"

    # create app desktop file /usr/share/applications/welcome.desktop
    cp -a "$pkgname.desktop" "$pkgname.desktop-enable"
    sed -i "$pkgname.desktop-enable" \
        -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
        -e 's|^#\(Exec=.* --once.*\)$|\1|'
    install -Dm644 "$pkgname.desktop-enable"  "$pkgdir/usr/share/applications/$pkgname.desktop"      # has --once
}
