# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=3.78
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    systemd eos-bash-shared eos-translations
    eos-quickstart
    eos-qogir-icons
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
  'kdiff3: alternative app for pacdiff to use'
  'meld: alternative app for pacdiff to use'
  'diffuse: alternative app for pacdiff to use'
  'vim: alternative app for pacdiff to use'
)

# url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
# _url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
# https://github.com/endeavouros-team/welcome/raw/main/LICENSE
# https://raw.githubusercontent.com/endeavouros-team/welcome/main/LICENSE
url=https://github.com/endeavouros-team/welcome
_url=https://raw.githubusercontent.com/endeavouros-team/welcome/main

source=(
  $_url/$pkgname
  $_url/$pkgname.bash-completion
  $_url/$pkgname.desktop
  $_url/wallpaper-once
  $_url/wallpaper-once.desktop
  $_url/eos-kill-yad-zombies
  $_url/welcome-dnd
  $_url/eos-install-mode-run-calamares
  $_url/arm-eos-welcome-installer
)
sha512sums=('2b5401c3ca56270d15e581a2ab3faee6abe8bed8d8be033b497155b7cf37759e47055869b2b4f491b5d1d9c47eaee727464b16a7af1494a60c9bd40a6a776358'
            'fba2533c23d530de23c900bc923c38a7ab4e17bd85e5e6bf34321403df014eac2b66f62771066748d49354d0d0df07c6c5190964b410f73f9d81793e998964d5'
            'fc360cf63894772bbc75f41d5b5f312ac4e70afb43bc081b63e3d8db6b8c9a1f0e08fa5a3fdff840f8064c883dda1d3f16328bfc8aa5b4a18e8adf83b93174a0'
            'acd528bafe16d61690d532961da562b0e9238153aa609c7d84d719a19525afe191b9cd54294b837d1d81c4d6cc6c9ae766712174aecb3708314e869addc6b537'
            'a193a605d95d837db568dab2feb074c035bea12bc7b08a39a7be3b075dd063cd05fb46b4bde5c86f81a26456c0aab3b0b834cb1ffd88820095a14291b4e059a5'
            '90dfc1f08a428787305d72a2e439a377aee0bc095fcb0a2efb4245a15285d4b96b5e0ac644c02f27ab68aebbfc5c64e6ab7d733d9db8952d2999d589410d3b9e'
            '9f0cd5edabf93439656046c60dc6a29f439a9d8b7afab8ec03b9470321ba98fc53b927944e2f68b5b152c150cca76913490eaab4256a5699da5960f860d81e81'
            '26d01c42749a723b74ab96099f6b5bd5f17ef6d5f0a451491bdf9f83016f4b19938b4714b13cc6bcf108b2ac1c5123f6c9338a499d17ce7cede00d3e6697443a'
            '6c9c83a0df23d0e290a8c077e1f606bfdb9cd4b226bad226eb14b787ea7d2246b5bf56dcb8f1fa06f332a1122ade83410e8e7879ad2a529345adec83eddac330')
package() {
    local -r _eosdir=usr/share/endeavouros/scripts
    local _src _name

    for _src in "${source[@]}" ; do
        _name=${_src##*/}
        case "$_name" in
            *.bash-completion)           install -Dm644 "$_name"  "$pkgdir/usr/share/bash-completion/completions/${_name%.*}" ;;
            *.desktop)                   install -Dm644 "$_name"  "$pkgdir/etc/xdg/autostart/$_name" ;;
            "$pkgname" | wallpaper-once) install -Dm755 "$_name"  "$pkgdir/$_eosdir/$_name" ;;
            *)                           install -Dm755 "$_name"  "$pkgdir/usr/bin/$_name" ;;
        esac
    done

    # create symlink: /usr/bin/eos-welcome -> /usr/share/endeavouros/scripts/welcome
    ln -s "/$_eosdir/$pkgname"   "$pkgdir/usr/bin/eos-$pkgname"

    # create app desktop file /usr/share/applications/welcome.desktop
    cp -a "$pkgname.desktop" "$pkgname.desktop-enable"
    sed -i "$pkgname.desktop-enable" \
        -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
        -e 's|^#\(Exec=.* --once.*\)$|\1|'
    install -Dm644 "$pkgname.desktop-enable"  "$pkgdir/usr/share/applications/$pkgname.desktop"      # has --once
}
