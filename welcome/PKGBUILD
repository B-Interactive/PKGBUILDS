# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=24.5
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    systemd eos-bash-shared eos-translations
    eos-quickstart
    eos-qogir-icons
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
  'kdiff3: alternative app for pacdiff to use'
  'meld: alternative app for pacdiff to use'
  'diffuse: alternative app for pacdiff to use'
  'vim: alternative app for pacdiff to use'
)

# url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
# _url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
# https://github.com/endeavouros-team/welcome/raw/main/LICENSE
# https://raw.githubusercontent.com/endeavouros-team/welcome/main/LICENSE
url=https://github.com/endeavouros-team/welcome
_url=https://raw.githubusercontent.com/endeavouros-team/welcome/main

source=(
  $_url/$pkgname
  $_url/${pkgname}-installer.conf
  $_url/$pkgname.bash-completion
  $_url/$pkgname.desktop
  $_url/wallpaper-once
  $_url/wallpaper-once.desktop
  $_url/eos-kill-yad-zombies
  $_url/welcome-dnd
  $_url/eos-install-mode-run-calamares
  $_url/arm-eos-welcome-installer
)
sha512sums=('dd064353657aa5d2abc2edc40e4d30b9d22bb1b70beedbc64a6388d92e732ce7ed42ef9ec69d3888c643f9007e8a2dcb5bbd4b5f0d5380b03420946857043455'
            'eb53ad0147ca8b13afc12589c2849227c590990538fcf08a1245b8ae224b6c1b547eec61b72c20314c67ab8c50068b0219d430b94079a932bfeb4824c7a1f1ba'
            '36dcdd5db1d86b88a87c4e48b718b0986107980fc289dd8dd643a365508255a81680d197442dfe6933bbb8b06e2ccc7d7b55bf28ce1a925cdfd0278c6fee2c7f'
            '5ca4b7108573a980d9d4011c5afde4f459abe8e36f5b6bc846368dcc48cfa2e672133f074fdeac4282835fa5b87f7afb2cd9dc3e985b4d2e8f574ac239c90ba2'
            'acd528bafe16d61690d532961da562b0e9238153aa609c7d84d719a19525afe191b9cd54294b837d1d81c4d6cc6c9ae766712174aecb3708314e869addc6b537'
            'a193a605d95d837db568dab2feb074c035bea12bc7b08a39a7be3b075dd063cd05fb46b4bde5c86f81a26456c0aab3b0b834cb1ffd88820095a14291b4e059a5'
            '90dfc1f08a428787305d72a2e439a377aee0bc095fcb0a2efb4245a15285d4b96b5e0ac644c02f27ab68aebbfc5c64e6ab7d733d9db8952d2999d589410d3b9e'
            '9f0cd5edabf93439656046c60dc6a29f439a9d8b7afab8ec03b9470321ba98fc53b927944e2f68b5b152c150cca76913490eaab4256a5699da5960f860d81e81'
            'b5c4ef7a68ab3a6d1290ad2e5c46d5827e22a675503b7c015319f1934d624d9f94a5c2c59ba14dde93c458770ed3b007d7cd6ad14c1496dac034676bd520d36d'
            'cbc39b4806cbfc6a81ddad7cc174d3850f82699c46c949a3ac9855f3c63f6cd6e91102ca357dc4176c7edcb02682e10b0175a95adcede81ec23a7ee84ab231aa')
package() {
    local -r _eosdir=usr/share/endeavouros/scripts
    local _src _name

    for _src in "${source[@]}" ; do
        _name=${_src##*/}
        case "$_name" in
            *.bash-completion)           install -Dm644 "$_name"  "$pkgdir/usr/share/bash-completion/completions/${_name%.*}" ;;
            *.desktop)                   install -Dm644 "$_name"  "$pkgdir/etc/xdg/autostart/$_name" ;;
            *.conf)                      install -Dm644 "$_name"  "$pkgdir/etc/$_name" ;;
            "$pkgname" | wallpaper-once) install -Dm755 "$_name"  "$pkgdir/$_eosdir/$_name" ;;
            *)                           install -Dm755 "$_name"  "$pkgdir/usr/bin/$_name" ;;
        esac
    done

    # create symlink: /usr/bin/eos-welcome -> /usr/share/endeavouros/scripts/welcome
    ln -s "/$_eosdir/$pkgname"   "$pkgdir/usr/bin/eos-$pkgname"

    # create app desktop file /usr/share/applications/welcome.desktop
    cp -a "$pkgname.desktop" "$pkgname.desktop-enable"
    sed -i "$pkgname.desktop-enable" \
        -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
        -e 's|^#\(Exec=.* --once.*\)$|\1|'
    install -Dm644 "$pkgname.desktop-enable"  "$pkgdir/usr/share/applications/$pkgname.desktop"      # has --once
}
