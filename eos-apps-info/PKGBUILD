# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=1.7
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  eos-bash-shared
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('8cefc007cd9ad7c1d53e096d14c52983ba56a8d53eb44b94d43101a227b8e301c8d1f500d67933ad0ffb958056712da20e1b9331ce6fefadca4a6d00c33694c3'
            'd21bee4142992604b9c030c9276b32e7a9f753df9e2ab423fc5f45941f5febaf923f894bd9bc7dac3ec5f9a7497a90456f43aeb492309f4e095ea059730fb538'
            '52ad986e08cf4bbfdf7d5fcc3bb255839287843ac6b5108c4cf9b18983872ef840bac39fe34a69512cfd6bdbff6cc156dd44fc1cc08156bc0e22e1d465c16b17'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            '4b5eef3ce297cfa66d38b6ad92739eee905050228f72bd2c58d7f05700269b0ef434b0aa5c6ff8423a98af33918089f1255858bd2385ef3db2967d71e0d1d350'
            '217f3fe6ab80788c34432bcee9f098edec35aa0c306572faf501e6f2299922dd25d2a5d1030dbf2923bb05eabf78dbed08c6e2fe73890f56f48b304ddc7efff8')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
