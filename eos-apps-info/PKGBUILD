# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=24.6.1
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  eos-bash-shared
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('be26da9037149c7801d9aa075bcf9e7feefe82aeba583587acb61e3dbf8fb2ae16e055dbd40e4003104860944a336c6e908d43db963de37aff71c0ab01b9ee18'
            '264b2ce9f3981728bb45e52ab9d2ec782a5e9a291ca2b9249a5558529f4717c868545524677185cb5cc2f370f373818dd2c690fa79f1a107c34c65b3d6ea53a0'
            '53e387eb1af9693dce6cc83636de399fc8ceedbce3e237c51c84e330766caec728808a2cf65ab653289b5da7c0648f47d17009f3e3669a15ddb0eb7772bae073'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            '4b5eef3ce297cfa66d38b6ad92739eee905050228f72bd2c58d7f05700269b0ef434b0aa5c6ff8423a98af33918089f1255858bd2385ef3db2967d71e0d1d350'
            '0c72506bf26f8baf878df56c5f327de14093da5f52c1e1710e51f83d272d1a1221ee8ca3fcf345a6a79e40e7accc06c9179da23966ad3e72d975fb69b8acfeb3')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
